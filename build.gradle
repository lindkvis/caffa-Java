apply plugin: 'java-library'
apply plugin: 'com.google.protobuf'
apply plugin: 'eclipse'

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.18'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    google()
}

def grpcVersion = "1.42.1"
def protobufVersion = "3.19.1"

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
    implementation "com.google.api.grpc:proto-google-common-protos:2.7.0"
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.slf4j:slf4j-api:1.7.28'
    implementation 'org.slf4j:slf4j-log4j12:1.7.28'
    implementation 'org.slf4j:jul-to-slf4j:1.7.29'
    implementation "io.grpc:grpc-alts:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.code.gson:gson:2.8.7"
    implementation "com.google.guava:guava:30.1.1-jre"
    compileOnly "javax.annotation:javax.annotation-api:1.2"
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(8))
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${protobufVersion}"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

jar {
  destinationDirectory = file('dist')
  exclude("*.java")
}
